### 1. 生成证书
使用命令
```
keytool -genkey -v -keystore dxl.jks -alias dxl_alias -keyalg RSA -validity 20000
```
解释：-genkey意味着执行的是生成数字证书操作，-v表示将生成证书的详细信息打印出来，显示在dos窗口中；-keystore dxl.jks 表示生成的数字证书的文件名为“dxl.jks”；-alias dxl.alias 表示证书的别名为“dxl.alias”，当然可以不和上面的文件名一样；-keyalg RSA 表示生成密钥文件所采用的算法为RSA；-validity 20000 表示该数字证书的有效期为20000天，意味着20000天之后该证书将失效。

生成的签名证书放到项目app目录。

### 2. 配置签名

在项目根目录添加文件`keystore.properties`，编辑内容：

```
keyAlias = dxl_alias
keyPassword = 123456
storeFile = dxl.jks
storePassword = 123456
```

配置build.gradle
```
import java.io.FileInputStream
import java.util.Properties

plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("kotlin-kapt")
}

val keystorePropertiesFile = rootProject.file("keystore.properties")
val keystoreProperties = Properties()
keystoreProperties.load(FileInputStream(keystorePropertiesFile))


android {
    namespace = "com.dxl.androidscaffold"
    compileSdk = 34

    defaultConfig {
        applicationId = "com.dxl.androidscaffold"
        minSdk = 21
        targetSdk = 34
        versionCode = 1
        versionName = "1.0"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures{
        viewBinding = true
        buildConfig = true
        dataBinding = true

    }

    signingConfigs {
        create("release") {
            keyAlias = keystoreProperties["keyAlias"] as String
            keyPassword = keystoreProperties["keyPassword"] as String
            storeFile = file(keystoreProperties["storeFile"] as String)
            storePassword = keystoreProperties["storePassword"] as String
            enableV1Signing = true
            enableV2Signing = true
            enableV3Signing = true
        }
    }

    buildTypes {
        getByName("release") {
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
            signingConfig =  signingConfigs.getByName("release")
        }

        getByName("debug") {
            isMinifyEnabled = false
            signingConfig =  signingConfigs.getByName("debug")
        }
    }



    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
}

dependencies {
    implementation("androidx.core:core-ktx:1.12.0")
    implementation("androidx.appcompat:appcompat:1.6.1")
    implementation("com.google.android.material:material:1.10.0")
    implementation("androidx.constraintlayout:constraintlayout:2.1.4")
    testImplementation("junit:junit:4.13.2")
    androidTestImplementation("androidx.test.ext:junit:1.1.5")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.5.1")

    implementation(project(mapOf("path" to ":scaffold")))

    implementation ("io.github.lucksiege:pictureselector:v3.11.1")


}

```

参考：https://dev.mi.com/distribute/doc/details?pId=1278
